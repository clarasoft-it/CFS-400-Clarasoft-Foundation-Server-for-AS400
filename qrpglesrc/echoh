
      /////////////////////////////////////////////////////////////////////////////////
      //
      //    Clarasoft Foundation Server 400
      //
      //    ECHOH
      //    Demonstration echo service called dynamically by clara daemon handler
      //    Version 1.0.0
      //
      //    Compile module with:
      //
      //       CRTRPGMOD MODULE(ECHOH) SRCFILE(QRPGLESRC)
      //                 SRCMBR(ECHOH) DBGVIEW(*ALL)
      //
      //    Build this as a service program program with:
      //
      //       CRTSRVPGM SRVPGM(ECHOH) MODULE(ECHOH)
      //       BNDSRVPGM(CFSAPI) EXPORT(*ALL)
      //
      //    This service program will be loaded by the CLARAH handler
      //    To use with the CLARAH handler, do the following:
      //
      //      1) Insert the following in the CFSREG file:
      //
      //         RGSRVNM: ECHOH  (or the name of the corresponding service in CFSCONF)
      //         RGLIBNM: <the library name where the ECHOH service pgm resides>
      //         RGPRCHD: ECHOH or the name of the service program
      //                          compiled from this source
      //         RGPRCNM: RUNECHO
      //         RGAPPID: <leave blank>
      //
      //      2) Execute the CLARAD daemon with the following command
      //         if you want to connect to the non secure handler:
      //
      //           call clarad parm('ECHOH')
      //
      //         Note: This assumes that the CFSCONF file has a record
      //         with the ECHOH key.
      //
      //     Distributed under the MIT license
      //
      //     Copyright (c) 2013 Clarasoft I.T. Solutions Inc.
      //
      //     Permission is hereby granted, free of charge, to any person obtaining
      //     a copy of this software and associated documentation files
      //     (the "Software"), to deal in the Software without restriction,
      //     including without limitation the rights to use, copy, modify,
      //     merge, publish, distribute, sublicense, and/or sell
      //     copies of the Software, and to permit persons to whom the Software is
      //     furnished to do so, subject to the following conditions:
      //     The above copyright notice and this permission notice shall be
      //     included in all copies or substantial portions of the Software.
      //     THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
      //     EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
      //     MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
      //     IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR
      //     ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
      //     TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH
      //     THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
      //
      /////////////////////////////////////////////////////////////////////////////////


     H NOMAIN

      /Copy cfsapih

      *----------------------------------------------------------------------
      * RUNECHO: non secure CFS echo server handler
      *----------------------------------------------------------------------

     PRUNECHO...
     P                 B                   EXPORT
     D                 PI            10I 0
     D@Conn                            *   Value

     DpCONN            S               *
     DFromCCSID        S              6A
     DToCCSID          S              6A
     DhResult          S             10I 0
     Dquit             S             10I 0
     DSize             S             20U 0
     DszMessage        S            256A
     DszResponse       S            256A
     Dconverter        S               *

     DSessionDS        DS                  LikeDS(SERVERSESSIONINFO_100)
     DServiceDS        DS                  LikeDS(CFS_SERVICEINFO_100)
     D                                     Based(@Conn)

      /Free

          Clear SessionDS;

          pCONN = CFS_OpenChannel(ServiceDS.conn_fd:
                                  %Addr(SessionDS):
                                  CFS_SERVERSESSION_FMT_100);

          If (pCONN <> *Null);

            converter = CSSTRCV_Constructor();

            hResult = CS_SUCCESS;
            Dou %bitand(hResult: CS_MASK_ERROR) <> CS_SUCCESS;

              Size = 255;
              szMessage = *Blanks;
              hResult = CFS_Read(pCONN:
                                 %Addr(szMessage):
                                 %Addr(Size):
                                 -1);

              If  %bitand(hResult: CS_MASK_ERROR) = CS_SUCCESS And
                  %bitand(hResult: CS_MASK_DIAG) <> CFS_DIAG_CONNCLOSE;

                ////////////////////////////////////////////////////////////
                // echo back:
                // this is just for demonstration purposes.
                // In this example, we assume the client
                // sends ASCII ... we will convert to EBCDIC
                // and then back to ASCII to send over to client.
                ////////////////////////////////////////////////////////////

                // null-terminate

                // Start a conversion from ASCII to the job CCSID
                FromCCSID = '00819' + x'00';
                ToCCSID = '00000' + x'00';
                CSSTRCV_SetConversion(converter:
                                      %Addr(FromCCSID): %Addr(ToCCSID));


                CSSTRCV_StrCpy(converter: %Addr(szMessage): Size);

                size = CSSTRCV_Size(converter);

                // We know that szClientByte is large enough ...
                szResponse = *Blanks;
                CSSTRCV_Get(converter: %Addr(szResponse));

                ////////////////////////////////////////////////////////////
                // Just to give a way for the client to
                // disconnect from this handler, if the first
                // character is the letter q, then we leave
                // the loop.
                ////////////////////////////////////////////////////////////

                quit = 0;

                szMessage = *Blanks;

                if %Trim(szResponse) = 'q';

                  quit = 1;
                  szMessage = 'ECHO HANDLER: GOODBYE :-)';

                Else;

                  szMessage = 'ECHO HANDLER: ' + %Trim(szResponse);

                EndIf;

                FromCCSID = '00000' + x'00';
                ToCCSID = '00819' + x'00';
                CSSTRCV_SetConversion(converter:
                                      %Addr(FromCCSID): %Addr(ToCCSID));

                CSSTRCV_StrCpy(converter:
                               %Addr(szMessage):
                               %Len(%Trim(szMessage)));

                // retrieve the converted string
                size = CSSTRCV_Size(converter);

                szResponse = *Blanks;
                CSSTRCV_Get(converter: %Addr(szResponse));

                // Send respopnse to client
                hResult = CFS_Write(pCONN:
                                    %Addr(szResponse):
                                    %Addr(size):
                                    -1);

                if quit = 1;
                  Leave;
                EndIf;
                
              Else;
              
                Leave;
                
              EndIf;

            EndDo;

            CFS_Close(pCONN);

          EndIf;

          Return 0;

      /End-Free

     P                 E

